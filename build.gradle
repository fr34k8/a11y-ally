buildscript {
    project.ext.KOTLIN_VERSION = '1.2.51'

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath 'com.android.tools.lint:lint-gradle:26.1.2'
        classpath 'com.noveogroup.android:check:1.2.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
    }
}

plugins {
    id 'com.quittle.setup-android-sdk' version '0.0.1'
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.noveogroup.android.check'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 16
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true
        disable 'GradleDependency', 'AllowBackup', 'GoogleAppIndexingWarning'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
}

check {
    abortOnError true
    checkstyle { config hard() }
    findbugs { config hard() }
    pmd { config hard() }
}

defaultTasks 'assemble', 'lint'

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        allWarningsAsErrors = true
    }
}
