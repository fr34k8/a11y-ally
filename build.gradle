buildscript {
    project.ext.KOTLIN_VERSION = '1.3.0'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.android.tools.lint:lint-gradle:26.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
    }
}

plugins {
    id 'com.quittle.setup-android-sdk' version '0.1.0'
    id 'com.vanniktech.code.quality.tools' version '0.13.0'
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

System.out.println('secret example: ' + System.getenv('SECRET').toUpperCase())
System.out.println('secret example length: ' + System.getenv('SECRET').length())

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    final String androidSigningPassword = System.getenv('ANDROID_SIGNING_PASSWORD')
    signingConfigs {
        if (androidSigningPassword) {
            release {
                storeFile file('config/a11y-ally-release.jks')
                storePassword androidSigningPassword
                keyPassword androidSigningPassword
                keyAlias 'a11y-ally-release-11/11/18'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')

            if (androidSigningPassword) {
                signingConfig signingConfigs.release
            } else {
                signingConfig debug.signingConfig
            }
        }
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true
        disable 'GradleDependency', 'AllowBackup', 'GoogleAppIndexingWarning'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$KOTLIN_VERSION"
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation 'androidx.fragment:fragment:1.0.0'
    testImplementation 'junit:junit:4.12'
}

codeQualityTools {
    htmlReports true
    xmlReports false

    detekt {
        config = 'config/detekt-config.yml'
    }

    findbugs {
        enabled = false // Fails the build without java input
        excludeFilter = 'config/findbugs-exclude-filter.xml'
    }
}

defaultTasks 'assemble', 'test', 'check'

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        allWarningsAsErrors = true
    }
}
