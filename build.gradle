buildscript {
    project.ext.KOTLIN_VERSION = '1.2.71'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.android.tools.lint:lint-gradle:26.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
    }
}

plugins {
    id 'com.quittle.setup-android-sdk' version '0.0.1'
    id 'com.vanniktech.code.quality.tools' version '0.13.0'
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 16
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true
        disable 'GradleDependency', 'AllowBackup', 'GoogleAppIndexingWarning'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation 'androidx.fragment:fragment:1.0.0'
    testImplementation 'junit:junit:4.12'
}

codeQualityTools {
    htmlReports true
    xmlReports false

    detekt {
        config = 'config/detekt-config.yml'
    }

    findbugs {
        enabled = false // Fails the build without java input
        excludeFilter = 'config/findbugs-exclude-filter.xml'
    }
}

defaultTasks 'assemble', 'test', 'check'

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        allWarningsAsErrors = true
    }
}
